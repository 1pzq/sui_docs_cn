---
title: 动态（对象）字段
description: Sui 上的动态字段和动态对象字段是动态添加和删除的，只有在访问时影响 gas，并存储异构值。
draft: true
---

使用对象字段存储原始数据和其他对象（封装）有各种方法，但是对这些方法有一些限制：

1. 对象具有由标识符键入的有限字段集，在发布其模块时是固定的（限于 `struct` 声明中的字段）。
2. 如果封装了多个其他对象，对象可能会变得非常庞大。较大的对象可能导致交易中的更高 gas 费用。此外，对象大小有一个上限。
3. 存在需要存储异构类型对象集合的用例。因为 Move `vector` 类型必须使用单一类型 `<T>` 实例化，因此它不适用于此。

幸运的是，Sui 提供了*动态字段*，其名称是任意的（不仅仅是标识符），可以在运行时添加和删除（不是在发布时固定），只有在访问时才会影响 gas，并且可以存储异构值。使用本主题中的库与此类字段进行交互。

### 字段与对象字段

有两种类型的动态字段——"字段" 和 "对象字段"——根据存储它们的值的方式而异：

- **字段** 可以存储具有 `store` 的任何值，但是存储在此类字段中的对象被视为已封装，外部工具（资源管理器、钱包等）无法通过其 ID 访问存储。
- **对象字段** 的值必须是对象（具有 `key` 能力，并且 `id: UID` 作为第一个字段），但仍然可以通过其 ID 对外部工具进行访问。

用于与这些字段进行交互的模块分别位于 [`dynamic_field`](https://github.com/MystenLabs/sui/tree/main/crates/sui-framework/packages/sui-framework/sources/dynamic_field.move) 和 [`dynamic_object_field`](https://github.com/MystenLabs/sui/tree/main/crates/sui-framework/packages/sui-framework/sources/dynamic_object_field.move)。

### 字段名称

与对象的常规字段不同，其中名称必须是 Move 标识符，动态字段的名称可以是具有 `copy`、`drop` 和 `store` 的任何值。这包括所有 Move 基本类型（整数、布尔值、字节字符串）和其内容都具有 `copy`、`drop` 和 `store` 的结构体。

### 添加动态字段

使用以下 API 添加动态字段：

```move
module sui::dynamic_field {

public fun add<Name: copy + drop + store, Value: store>(
  object: &mut UID,
  name: Name,
  value: Value,
);

}
```

```move
module sui::dynamic_object_field {

public fun add<Name: copy + drop + store, Value: key + store>(
  object: &mut UID,
  name: Name,
  value: Value,
);

}
```

这些函数将名为 `name` 且值为 `value` 的字段添加到 `object`。要查看其运行效果，请考虑以下代码片段：

首先，定义父对象和子对象的两种对象类型：

```move
struct Parent has key {
    id: UID,
}

struct Child has key, store {
    id: UID,
    count: u64,
}
```

接下来，定义一个将 `Child` 对象作为 `Parent` 对象的动态字段添加的 API：

```move
use sui::dynamic_object_field as ofield;

public fun add_child(parent: &mut Parent, child: Child) {
    ofield::add(&mut parent.id, b"child", child);
}
```

此函数通过值获取 `Child` 对象，并将其作为具有名称 `b"child"`（一个 `vector<u8>` 类型的字节字符串）的动态字段添加到 `parent`。此调用导致以下所有权关系：

1. 发件人地址（仍然）拥有 `Parent` 对象。
2. `Parent` 对象拥有 `Child` 对象，并且可以通过名称 `b"child"` 引用它。

尝试覆盖字段（尝试添加具有已定义的相同 `<Name>` 类型和值的字段）会导致错误，并且事务将失败。你可以通过可变引用对其进行原地修改，也可以通过首先删除旧值来安全地覆盖它们（例如更改其值类型）。

### 访问动态字段

你可以使用以下 API 通过引用引用动态字段：

```move
module sui::dynamic_field {

public fun borrow<Name: copy + drop + store, Value: store>(
    object: &UID,
    name: Name,
): &Value;

public fun borrow_mut<Name: copy + drop + store, Value: store>(
    object: &mut UID,
    name: Name,
): &mut Value;

}
```

其中 `object` 是字段定义的对象的 UID，`name` 是字段的名称。

:::info

`sui::dynamic_object_field` 具有等效的函数，但是对于对象字段，有一个额外的约束 `Value: key + store`。

:::

要在先前定义的 `Parent` 和 `Child` 类型中使用这些 API：

```move
use sui::dynamic_object_field as ofield;

public fun mutate_child(child: &mut Child) {
    child.count = child.count + 1;
}

public fun mutate_child_via_parent(parent: &mut Parent) {
    mutate_child(ofield::borrow_mut(
        &mut parent.id,
        b"child",
    ));
}
```

第一个函数直接接受对 `Child` 对象的可变引用，你可以使用尚未添加为 `Parent` 对象字段的 `Child` 对象调用它。

第二个函数接受对 `Parent` 对象的可变引用，并使用 `borrow_mut` 访问其动态字段，然后传递给 `mutate_child`。这只能在已定义 `b"child"` 字段的 `Parent` 对象上调用。已添加到 `Parent` 的 `Child` 对象必须通过其动态字段访问，因此只能使用 `mutate_child_via_parent` 而不能使用 `mutate_child` 进行修改，即使其 ID 已知。

:::tip

如果尝试借用不存在的字段，则事务将失败。

:::

传递给 `borrow` 和 `borrow_mut` 的 `<Value>` 类型必须与存储字段的类型匹配，否则事务将中止。

必须通过这些 API 访问动态对象字段的值。如果尝试将这些对象用作输入（通过值或引用）的事务，则会因为具有无效输入而被拒绝。

### 移除动态字段

类似于解包常规字段中保存的对象，你可以删除动态字段，暴露其值：

```move
module sui::dynamic_field {

public fun remove<Name: copy + drop + store, Value: store>(
    object: &mut UID,
    name: Name,
): Value;

}
```

此函数接受对 `object` 的 UID 的可变引用以及字段的 `name`。如果在 `object` 上以 `name` 定义了一个具有 `value: Value` 的字段，则删除该字段并返回 `value`，否则中止。将来尝试在 `object` 上访问此字段将失败。

:::tip

`sui::dynamic_object_field` 有一个等效的函数用于对象字段。

:::

返回的值可以像任何其他值一样进行交互（因为它就是任何其他值）。例如，然后可以对已移除的动态对象字段值执行 `delete` 或 `transfer` 操作到某个地址（返回给发送方）：

```move
use sui::dynamic_object_field as ofield;
use sui::{object, transfer, tx_context};
use sui::tx_context::TxContext;

public fun delete_child(parent: &mut Parent) {
    let Child { id, count: _ } = reclaim_child(parent);

    object::delete(id);
}

public fun reclaim_child(parent: &mut Parent, ctx: &mut TxContext): Child {
    ofield::remove(
        &mut parent.id,
        b"child",
    );

}
```

类似于借用字段，尝试移除不存在的字段或带有不同 `Value` 类型的字段的事务将失败。

### 删除具有动态字段的对象

可以删除仍然在其上定义有（可能是非 `drop` 的）动态字段的对象。因为字段值只能通过动态字段的关联对象和字段名称访问，所以删除具有仍然在其上定义有动态字段的对象会使它们在将来的事务中全部无法访问。无论字段的值是否具有 `drop` 能力，这都是正确的。当向对象添加少量静态已知的附加字段时，这可能不是一个问题，但对于可能包含作为动态字段的键值对的无限多个集合类型而言，这是非常不希望的。

Sui 提供了使用动态字段构建的 `Table` 和 `Bag` 集合，但是还提供了额外的支持，以计算它们包含的条目数，以防止在非空时意外删除。要了解更多信息，请参见[Tables and Bags](./tables-bags.mdx)。
